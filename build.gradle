plugins {
    id 'java'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'com.dreamer.pokemonmod'
version = '0.0.1'

def targetJavaVersion = 1.8
java {
    archivesBaseName = 'pokemonmod'
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // official   MCVersion             Official field/method names from Mojang mapping files
    // parchment  YYYY.MM.DD-MCVersion  Open community-sourced parameter names and javadocs layered on top of official
    //
    // You must be aware of the Mojang license when using the 'official' or 'parchment' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Parchment is an unofficial project maintained by ParchmentMC, separate from MinecraftForge
    // Additional setup is needed to use their mappings: https://github.com/ParchmentMC/Parchment/wiki/Getting-Started
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: '1.16.5'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'


            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'


            mods {
                pokemonmod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'


            mods {
                pokemonmod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'pokemonmod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                pokemonmod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use. If this is any group other than 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency, and its patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'

    implementation fg.deobf('pixelmon:Pixelmon-1.16.5-9.1.7-universal:9.1.7')
//    implementation files('libs/Pixelmon-1.16.5-9.1.7-universal.jar')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveName = rootProject.name + '-Forge.jar'
    dependsOn('reobfJar')

    // Where you relocate any shaded dependencies

    exclude 'net.minecraft'
    exclude "**/module-info.class"
}

reobf {
    shadowJar {}
}

task versionedRelease(type: Copy) { // This creates a jar with a version number appended to the end in the `release` directory (for ease of use)
    delete fileTree('release/') {
        include '**/*.jar'
    }
    group "build"
    from('./build/libs/' + rootProject.name + '-Forge.jar')
    into('release/')
    include '*.jar'
    rename { String filename ->
        filename.replace(".jar", "-${project.version}.jar")
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
//    // Put repositories for dependencies here
//    // ForgeGradle automatically adds the Forge maven and Maven Central for you
//    mavenCentral()
//    maven {
//        name = 'sonatype'
//        url = 'https://oss.sonatype.org/content/groups/public/'
//    }
//    maven {
//        name = 'spigotmc-repo'
//        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
//    }
//    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
//    flatDir {
//        dirs './libs'
//    }
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.lucko.me/' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://jitpack.io" }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    ivy {
        setUrl('https://download.nodecdn.net/containers/reforged/universal/release')
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact('[artifact].[ext]')
        }
    }
}

javadoc {
    options {
        links 'https://reforged.gg/docs/1165/'
    }
}

jar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')
